/*
 * Copyright 2015 Mark Michaelis
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

repositories {
  mavenCentral()
}

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    // Axion Release Plugin: https://github.com/allegro/axion-release-plugin
    classpath group: 'pl.allegro.tech.build', name: 'axion-release-plugin', version: '1.2.3'
  }
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'pl.allegro.tech.build.axion-release'

scmVersion {
  tag {
    prefix = project.name + ".release"
  }
  hooks {
    pre 'fileUpdate',
        [file: 'README.md', pattern: {v, p -> /Version: $v/}, replacement: {v, p -> "Version: $v"}]
    pre 'fileUpdate',
        [files: fileTree('src').include('**/*.java'), pattern: {v, p -> /SINCE/}, replacement: {
          v, p -> "$v"
        }]
    pre 'commit', {v, p -> "Applying release version $v."}
  }
}

project.group = 'com.github.mmichaelis'
// Version determined by Axion Release Plugin
project.version = scmVersion.version

/* define the valid syntax level for source files */
sourceCompatibility = JavaVersion.VERSION_1_7
/* define binary compatibility version */
targetCompatibility = JavaVersion.VERSION_1_7

def isDevBuild
def isCiBuild
def isReleaseBuild

def sonatypeRepositoryUrl

compileJava.doFirst {
  /* trying to debug https://github.com/mmichaelis/hamcrest-nextdeed/issues/1 */
  logger.info("Source compatibility {}", sourceCompatibility)
  logger.info("Target compatibility {}", targetCompatibility)
}

logger.info(
    "Determined Project information: " + project.group + ":" + project.name + ":" + project.version)

if (hasProperty("local")) {
  logger.info("Deployment (uploadArtifacts) will go to local maven repository.")
  isDevBuild = true
} else if (project.version.toString().endsWith('SNAPSHOT')) {
  logger.info("Deployment (uploadArtifacts) will go to Sonatype Snapshots.")
  isCiBuild = true
  sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
  logger.info("Release Build enabled. Deployment (uploadArtifacts) will go to Sonatype Staging.")
  isReleaseBuild = true
  sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

dependencies {
  compile(
      [group: 'org.hamcrest', name: 'java-hamcrest', version: '2.0.0.0'],
      [group: 'org.jetbrains', name: 'annotations', version: '13.0']
  )
  testCompile(
      [group: 'junit', name: 'junit', version: '4.12', transitive: false],
      [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.10']
  )
  testRuntime(
      [group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2']
  )
}

jar {
  manifest {
    attributes 'Implementation-Title': 'Hamcrest - Next Deed',
               'Implementation-Vendor': 'Mark Michaelis',
               'Implementation-Version': version
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives jar

  archives javadocJar
  archives sourcesJar
}

if (isReleaseBuild) {
  signing {
    sign configurations.archives
  }
} else {
  task signArchives {
    logger.info("Signing skipped as release build not triggered.")
    // do nothing
  }
}

uploadArchives {
  repositories {
    if (isDevBuild) {
      mavenLocal()
    } else {
      mavenDeployer {
        beforeDeployment {
          MavenDeployment deployment -> signing.signPom(deployment)
        }
        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }
        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }
        pom.project {
          name project.name
          packaging 'jar'
          description 'Hamcrest - Next Deed is an extension to Hamcrest matchers.'
          url 'https://github.com/mmichaelis/hamcrest-nextdeed'
          scm {
            url 'https://github.com/mmichaelis/hamcrest-nextdeed'
            connection 'git@github.com:mmichaelis/hamcrest-nextdeed.git'
            developerConnection 'git@github.com:mmichaelis/hamcrest-nextdeed.git'
          }
          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution 'repo'
            }
          }
          developers {
            developer {
              id 'mmichaelis'
              name 'Mark Michaelis'
            }
            developer {
              id 'okummer'
              name 'Olaf Kummer'
            }
          }
        }
      }
    }
  }
}

task wrapper(type: Wrapper) {
  group = 'Buildmaster'
  description = 'Generates gradlew and gradlew.bat bootstrap scripts'
  gradleVersion = '2.4'
}
